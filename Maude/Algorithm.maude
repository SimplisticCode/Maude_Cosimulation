load Port

(fmod ALGEBRAICLOOP is
    protecting Ports .
    sort AlgebraicLoop LoopType .
    ops feedthrough reactivity : -> LoopType [ctor] . 
    op AlgebraicLoop:_Type:_ : PortSet LoopType -> AlgebraicLoop [ctor] . 
endfm )

(view ALGEBRAICLOOPAsTRIV from TRIV to ALGEBRAICLOOP is
  protecting ALGEBRAICLOOP .
  sort Elt to AlgebraicLoop .
endv)

(fmod ALGEBRAICLOOPSET is
  protecting SET{ALGEBRAICLOOPAsTRIV} * (sort NeSet{ALGEBRAICLOOPAsTRIV} to NeALSet, sort Set{ALGEBRAICLOOPAsTRIV} to AlgebraicLoopSet) .
endfm)



*** TODO look at action list to make it to a proper list
(omod Algorithm is 
 protecting Ports + ALGEBRAICLOOPSET .
 sorts ActionType FMIActionType ComplexActionType ActionList Action  .
 subsort Action < ActionList .
    
 *** Types
 ops CreateExp EnterInitialization ExitInitialization Terminate FreeInstance Unload : -> FMIActionType [ctor] . 
 ops Set Get Step Save : -> ActionType [ctor] . 
 ops StepNegotiation Loop : -> ComplexActionType [ctor] . 
 op fmiEvent:_SUs:_ : FMIActionType NeOidSet -> Action [ctor format(nir! d d d d)] .
 op portEvent:_SU:_PId:_ : ActionType SUID OidSet -> Action [ctor format(nib! d d d d d d)] .
 op stepEvent:_StepSize:_ : SUID NzNat -> Action [ctor format(nig! d d d d)] .
 op complexEvent:_SaveSUs:_RestoreSUs:_Actions:_ : ComplexActionType OidSet OidSet ActionList -> Action [ctor format(niy! d d d d d d ni! niy!)] .
 
 *** Operations
 op _memberOf_ : EPortId AlgebraicLoopSet -> Bool . 
 op getSUs : PortSet OidSet -> OidSet .
 op emptyList : -> ActionList [ctor] .
 op _;_ : ActionList ActionList -> ActionList [ctor assoc id: emptyList] .

class OrchestrationData | 
    Algorithm : ActionList, 
    Initialization : ActionList,
    Termination : ActionList,
    SNSet : OidSet,
    defaultStepSize : NzNat,
    actualStepSize : NzNat,
    endTime : NzNat,
    unsolvedSCC : AlgebraicLoopSet,
    solvedSCC : AlgebraicLoopSet,
    guessOn : PortSet,
    valuesToSet : PortValueMap,
    simulationTime : Nat,
    suids : NeOidSet .

 var ID : SUID .
 var SUIDs : OidSet .
 var LOOPS : AlgebraicLoopSet .
 var PORT : EPortId .
 var LOOPTYPE : LoopType .
 var PORTSET : PortSet .
 var PORTID : PortId .
 vars ENDTIME STEPSIZE : NzNat .

 eq PORT memberOf ((AlgebraicLoop: (PORT , PORTSET)  Type: LOOPTYPE) , LOOPS) = true .
 eq PORT memberOf LOOPS = false [owise] .

 eq getSUs(empty, SUIDs) = SUIDs .
 ceq getSUs(((ID ! PORTID), PORTSET), SUIDs) = getSUs(PORTSET, SUIDs) if ID in SUIDs .
 eq getSUs(((ID ! PORTID), PORTSET), SUIDs) = getSUs(PORTSET, (ID , SUIDs)) [owise] .

 op initialOrchestrationData : NzNat NzNat AlgebraicLoopSet NeOidSet -> Object .
  eq initialOrchestrationData(STEPSIZE, ENDTIME, LOOPS, SUIDs) =
    < "Orchestrator" : OrchestrationData | 
      Algorithm : emptyList, 
      Initialization : emptyList,
      Termination : emptyList,
      SNSet : empty,
      defaultStepSize : STEPSIZE,
      actualStepSize : STEPSIZE,
      endTime : ENDTIME,
      unsolvedSCC : LOOPS,
      solvedSCC : empty,
      guessOn : empty,
      valuesToSet : empty,
      simulationTime : 0,
      suids : SUIDs > .

endom)

