load Algorithm

(omod SimulationUnit is
   protecting Algorithm + RANDOM + STRING + LOCALSTATE .
   sort fmiState StepState .
   
   *** The different states of the FMI standard
   ops Instantiated ExperimentSetup Initialize Simulation Terminated InstanceFreed Unloaded : -> fmiState [ctor] . 


   ***Classes
   class SU |
     path : String, 
     time : Nat, 
     inputs : Configuration, ***Input ports
     outputs : Configuration, ***Output ports
     canReject : Bool, 
     fmistate : fmiState,
     parameters : LocalState,
     localState : LocalState .
     *** Add local state
     *** Parameters
     *** Local State Map<String, FMIValue >

   op askStepSize : Object NzNat -> NzNat .
   op feedthroughSatisfied : OidSet Configuration Nat -> Bool .
   op allDef : Configuration Nat -> Bool .
   op undefPorts : Configuration Nat -> Configuration .
   op allInputsDef : Configuration Nat Nat -> Bool .
   op allInputsSet : Configuration -> Bool .
   op canStep : Nat NzNat Configuration Configuration -> Bool .
   op getAction : Object OidSet -> Object .
   op setAction : Object OidSet FMIValue Nat -> Object .
   op stepAction : Object NzNat -> Object .
   op getAllPorts : Configuration PortSet -> PortSet .
   op getAllReactivePorts : Configuration PortSet -> PortSet .
  *** SU -> Values of the input ports -> 

   vars ID ID1 ID2 : SUID .
   vars PORTID INPUT OUTPUT : PortId .
   vars TIME SUTIME VALVESTATE WATERLEVEL : Nat .
   vars CONF INPUTS OUTPUTS : Configuration .
   var VALUE : FMIValue .
   var PORTSTATUS : PortStatus .
   vars FEEDTHROUGH PORTS : OidSet .
   vars FUTURETIME STEP : NzNat .
   var PORTSET : PortSet .
   var VALUEMAP : PortValueMap .
   var CONTRACT : Contract .

   *** Get All ports in the scenario:
  ceq getAllPorts(< ID1 : SU | inputs : (< INPUT : Input | > INPUTS) > CONF, PORTSET) = 
    getAllPorts(< ID1 : SU | inputs : INPUTS > CONF,  ((ID1 ! INPUT) , PORTSET))
    if not (ID1 ! INPUT) in PORTSET .

  ceq getAllPorts(< ID1 : SU | outputs : (< OUTPUT : Output | > OUTPUTS) > CONF, PORTSET) = 
    getAllPorts(< ID1 : SU | outputs : OUTPUTS > CONF, ((ID1 ! OUTPUT) , PORTSET))
      if not (ID1 ! OUTPUT) in PORTSET .

  eq getAllPorts(CONF, PORTSET) = PORTSET [owise] . 

  ceq getAllReactivePorts(< ID1 : SU | inputs : (< INPUT : Input | contract : reactive > INPUTS) > CONF, PORTSET) = getAllReactivePorts(< ID1 : SU | inputs : INPUTS > CONF, ((ID1 ! INPUT) , PORTSET))
    if not (ID1 ! INPUT) in PORTSET .
  eq getAllReactivePorts(CONF, PORTSET) = PORTSET [owise] . 

   *** Checks if all feedthrough constraints are satisfied
   eq feedthroughSatisfied(empty, CONF, TIME) = true .
   eq feedthroughSatisfied((PORTID , FEEDTHROUGH), < PORTID : Input | status : Undef > CONF, TIME) = false .
   ceq feedthroughSatisfied((PORTID , FEEDTHROUGH), < PORTID : Input | time : TIME > CONF, SUTIME) = false 
    if TIME =/= SUTIME .
   eq feedthroughSatisfied((PORTID , FEEDTHROUGH), < PORTID : Input | time : TIME, status : Def > CONF, TIME) =  feedthroughSatisfied(FEEDTHROUGH, CONF, TIME) .

   eq allDef(none, TIME) = true .
   eq allDef(< PORTID : Port | status : PORTSTATUS, time : TIME > CONF, TIME) = PORTSTATUS == Def and allDef(CONF, TIME) .

*** Are not defined for all cases - need to be redefined
   eq allInputsDef(none, SUTIME, FUTURETIME) = true .
   eq allInputsDef(< PORTID : Input | time : TIME, contract : CONTRACT, status : PORTSTATUS > CONF, SUTIME, FUTURETIME) = PORTSTATUS == Def and ((CONTRACT == reactive and TIME == FUTURETIME) or (CONTRACT == delayed and TIME == SUTIME))
        and allInputsDef(CONF, SUTIME, FUTURETIME) .

   eq undefPorts(none, TIME) = none .
   eq undefPorts(< PORTID : Port | > CONF, TIME) =  < PORTID : Port | value : < random(TIME) >, status : Undef, time : TIME > undefPorts(CONF, TIME) .

   eq allInputsSet(none) = true .
   eq allInputsSet(< ID : SU | inputs : INPUTS > CONF) = allDef(INPUTS, 0) and allInputsSet(CONF) .

   eq canStep(SUTIME, FUTURETIME, INPUTS, OUTPUTS) = 
      allInputsDef(INPUTS, SUTIME, FUTURETIME) and allDef(OUTPUTS, SUTIME) . 

   *** For the moment we allow all steps - should updated to reflect the nondeterminism of the step negotiation!
   eq askStepSize(< ID : SU | >, STEP) = STEP .

  *** Updates the time and status of the outputs
  eq getAction(< ID : SU | >, empty) = < ID : SU | > .
  eq getAction(< ID : SU | time : TIME, outputs : (< OUTPUT : Output | > OUTPUTS) >, (OUTPUT , PORTS)) = 
    getAction(< ID : SU | outputs : (< OUTPUT : Output | time : TIME, status : Def > OUTPUTS) >, PORTS) .


  eq setAction(< ID : SU | > , empty, VALUE, TIME) =  < ID : SU | > .
  eq setAction(< ID : SU | inputs : (< INPUT : Input | > INPUTS) > , (INPUT , PORTS), VALUE, TIME) = 
            setAction(< ID : SU | inputs : (< INPUT : Input | time : TIME, value : VALUE,  status : Def > INPUTS) >, PORTS, VALUE, TIME) .

  *** Updates the time and the status of the outputs
  ceq stepAction(< ID : SU | time : TIME, outputs : OUTPUTS >, STEP) 
    = < ID : SU | time : FUTURETIME, outputs : undefPorts(OUTPUTS, FUTURETIME) > 
    if FUTURETIME := TIME + STEP .

  op step : Object NzNat -> StepState [ctor] .
  subsort Object < StepState .

endom)