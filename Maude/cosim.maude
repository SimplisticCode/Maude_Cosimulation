load StepFinder

(omod COSIMULATION is 
   protecting StepFinder .
   sort GlobalState .

   op performStepNegotiation : Configuration -> Configuration .
   op performGlobalStep : Configuration -> Configuration .
   op allSUinSimulation : Configuration -> Bool .
   op `{_`} : Configuration -> GlobalState .
   op getPorts : Configuration EPortIdSet -> EPortIdSet .
   op simulationStepDone : Configuration -> Bool .

   vars ID1 ID2 : SUID .
   var SUIDs : SUIdSet .
   vars I O : PortId .
   vars TIME TIME1 TIME2 : Nat .
   vars INPUTS OUTPUTS C : Configuration . 
   var STATE : fmiState .
   var ALGO : ActionList .
   var FEEDTHROUGH : PortIdSet .
   var PORTS : EPortIdSet .
   vars STEPSIZE FUTURETIME ENDTIME : NzNat .
   var LOOP : Algebraic .  
   var LOOPS : AlgebraicLoops .  
   var INPUTTYPE : Mode .
   var STEPRes : findStepPair .

  eq simulationStepDone(< ID1 : SU | time : TIME, inputs : INPUTS, outputs : OUTPUTS > C) = TIME > 0 and allDef(INPUTS, TIME) and allDef(OUTPUTS, TIME) and simulationStepDone(C) .
  eq simulationStepDone(C) = true [owise] .

  *** Step Negotiation
  ceq performStepNegotiation(C SNSet: SUIDs stepSize: STEPSIZE Algorithm: ALGO) = 
                              C SNSet: emptySUSet 
                              Algorithm: (ALGO ; (complexEvent: StepNegotiation SaveSUs: SUIDs RestoreSUs: SUIDs Actions: getAL(STEPRes)))
                              stepSize: getStep(STEPRes)
                              if needsStepNegotiation(SUIDs) /\ STEPRes := findStep(SUIDs, C stepSize: STEPSIZE, C stepSize: STEPSIZE, emptyList) .

  ceq performStepNegotiation(C SNSet: SUIDs) = C if not needsStepNegotiation(SUIDs) .  

  *** Get All ports in the scenario:
  ceq getPorts(< ID1 : SU | inputs : (< I : Input | > INPUTS) > C, PORTS) = getPorts(< ID1 : SU | > C,  (ID1 ! I) ;; PORTS )
            if not (ID1 ! I) in PORTS .

  ceq getPorts(< ID1 : SU | outputs : (< O : Output | > OUTPUTS) > C, PORTS) = getPorts(< ID1 : SU | > C,  (ID1 ! O) ;; PORTS )
            if not (ID1 ! O) in PORTS .

  eq getPorts(C, PORTS) = PORTS [owise] . 

  *** All SUs are in the simulation state
  eq allSUinSimulation(< ID1 : SU | state : STATE > C) = STATE == Simulation and allSUinSimulation(C) . 
  eq allSUinSimulation(C) = true [owise] .

  *** Perform the global simulation step
  *** Set input
  ceq performGlobalStep(C < ID1 : SU | time : TIME, inputs : (< I : Input | time : TIME1, type : INPUTTYPE > INPUTS) > 
                          < ID2 : SU | outputs : (< O : Output | time : TIME2, status : Def > OUTPUTS) > 
                         (ID2 ! O ==> ID1 ! I) Algorithm: ALGO) = 
            performGlobalStep(C setAction( < ID1 : SU | >, I, TIME2) < ID2 : SU | > 
                         (ID2 ! O ==> ID1 ! I) 
                         Algorithm: (ALGO ; (event: Set SU: ID1 PId: I)))
                         if TIME1 < TIME2 and ((INPUTTYPE == d and TIME1 < TIME) or (INPUTTYPE == r and TIME == TIME1)) .

  *** Step
  ceq performGlobalStep(C < ID1 : SU | time : TIME, inputs : INPUTS, outputs : OUTPUTS > stepSize: STEPSIZE endTime: ENDTIME Algorithm: ALGO)  = 
      performGlobalStep(C 
                        stepAction(< ID1 : SU | >, STEPSIZE)
                        stepSize: STEPSIZE endTime: ENDTIME Algorithm: (ALGO ; (event: Step SU: ID1 PId: emptySet)))
                        if FUTURETIME := TIME + STEPSIZE /\ canStep(TIME, FUTURETIME, INPUTS, OUTPUTS) and FUTURETIME <= ENDTIME . 

  *** Get
  ceq performGlobalStep(C < ID1 : SU | time : TIME, inputs : INPUTS, outputs : (< O : Output | status : Undef, dependsOn : FEEDTHROUGH > OUTPUTS) >       Algorithm: ALGO) =
                    performGlobalStep(C getAction(< ID1 : SU | >, O) Algorithm: (ALGO ; (event: Get SU: ID1 PId: O)))  
                    if feedthroughSatisfied(FEEDTHROUGH, INPUTS, TIME) .

  *** Algebraic Loop - solve and remove loop
  eq performGlobalStep(C SCC: (LOOP # LOOPS)) = solveLoop(C SCC: LOOPS, LOOP) [owise] .

  *** 
  ceq performGlobalStep(C) = C if simulationStepDone(C) .

  *** FMI-standard state transition:
  rl [setExp] : < ID1 : SU | state : Instantiated > Algorithm: ALGO 
                 => < ID1 : SU | state : ExperimentSetup > Algorithm: (ALGO ; (fmiEvent: CreateExp SU: ID1)) .

  rl [init] : < ID1 : SU | inputs : INPUTS , outputs : OUTPUTS, state : ExperimentSetup >  Algorithm: ALGO 
           => < ID1 : SU | inputs : undefPorts(INPUTS, 0), outputs : undefPorts(OUTPUTS, 0), state : Initialize > 
              Algorithm: (ALGO ; (fmiEvent: EnterInitialization SU: ID1)).

  crl [simulate] : < ID1 : SU | inputs : INPUTS , outputs : OUTPUTS, state : Initialize > C Algorithm: ALGO 
                => < ID1 : SU | state : Simulation > C Algorithm: (ALGO ; (fmiEvent: ExitInitialization SU: ID1))
                if allDef(INPUTS, 0) and allDef(OUTPUTS, 0) and allInputsSet(C) .

  crl [terminate] : < ID1 : SU | time : TIME, inputs : INPUTS , outputs : OUTPUTS , state : Simulation > endTime: TIME Algorithm: ALGO 
                 => < ID1 : SU | state : Terminated > endTime: TIME Algorithm: (ALGO ; (fmiEvent: Terminate SU: ID1))
                 if allDef(OUTPUTS, TIME) and allDef(INPUTS, TIME) .

  rl [free] : < ID1 : SU | state : Terminated > Algorithm: ALGO 
           => < ID1 : SU | state : InstanceFreed > Algorithm: (ALGO ; (fmiEvent: FreeInstance SU: ID1)) .

  rl [unload] : < ID1 : SU | state : InstanceFreed > Algorithm: ALGO
              => < ID1 : SU | state : Unloaded > Algorithm: (ALGO ; (fmiEvent: Unload SU: ID1)) .


   *** Initalization Procedure 
   crl [get-init] :  < ID1 : SU | outputs : ((< O : Output | status : Undef , dependsOn : FEEDTHROUGH >) OUTPUTS) , inputs : INPUTS, state : Initialize > Algorithm: ALGO 
                  => 
                  getAction(< ID1 : SU | >, O)                  
                  Algorithm: (ALGO ; (event: Get SU: ID1 PId: O)) if feedthroughSatisfied(FEEDTHROUGH, INPUTS, 0) .

   *** Inputs can only rely on one connection 
   rl [set-init] :  < ID1 : SU | inputs : (< I : Input | status : Undef > INPUTS), state : Initialize > Algorithm: ALGO 
                    < ID2 : SU | outputs : (< O : Output | status : Def > OUTPUTS) > (ID2 ! O ==> ID1 ! I)
                        => 
                           setAction(< ID1 : SU | >, I, 0)
                           < ID2 : SU | > (ID2 ! O ==> ID1 ! I) Algorithm: (ALGO ; (event: Set SU: ID1 PId: I)) .


  ***rl [init-loop] : C SCC: (L # LOOPS) => solveLoop(C SCC: LOOPS, L) .                 

 ***Complex scenarios with save and restore - this should 

  ***If the scenario contains SUs that require step negotiation
  ***The step negotiation
  crl [stepNego] : { C SNSet: SUIDs} => 
          { performStepNegotiation(C SNSet: SUIDs)}
          if needsStepNegotiation(SUIDs) and allSUinSimulation(C) .


  crl [simulationStep]: { C SNSet: SUIDs} => 
          { performGlobalStep(C SNSet: SUIDs) }
        if (not needsStepNegotiation(SUIDs)) and allSUinSimulation(C) .

   *** Definition of system
  op externalConnection : ->  Configuration .    *** External Connections - all ports have unique name
  op simulationUnits : -> Configuration .      *** SUs - ID * input ports * and outputs


***Encoding of the Scenario 
eq externalConnection = ('msd1 ! 'x1 ==> 'msd2 ! 'x1) ('msd1 ! 'v1 ==> 'msd2 ! 'v1) ('msd2 ! 'fk ==> 'msd1 ! 'fk) ('msd2 ! 'z ==> 'msd3 ! 'z) ('msd3 ! 'G ==> 'msd2 ! 'G) .

eq simulationUnits = (< 'msd3 : SU | time : 0, inputs : (< 'z : Input | time : 0, type : d, status : Undef  >), outputs : (< 'G : Output | time : 0, status : Undef, dependsOn : ( 'z ) >), state : Instantiated, canReject : true >)

 	 (< 'msd2 : SU | time : 0, inputs : (< 'v1 : Input | time : 0, type : r, status : Undef  >) (< 'x1 : Input | time : 0, type : d, status : Undef  >) (< 'G : Input | time : 0, type : r, status : Undef  >), outputs : (< 'fk : Output | time : 0, status : Undef, dependsOn : ( 'x1 :: 'v1 ) >) (< 'z : Output | time : 0, status : Undef, dependsOn : emptySet >), state : Instantiated, canReject : true >)

 	 (< 'msd1 : SU | time : 0, inputs : (< 'fk : Input | time : 0, type : r, status : Undef  >), outputs : (< 'x1 : Output | time : 0, status : Undef, dependsOn : emptySet >) (< 'v1 : Output | time : 0, status : Undef, dependsOn : emptySet >), state : Instantiated, canReject : true >) .



endom)

eof

*** Check for termination - not useful 
(search setup =>! X:Configuration . )
show search graph .

( frew [20] setup . )

( frew [20] setup using fmi_rules_setExpt . )
( red needsStepNegotiation(setup) . )
( red allSUinSimulation(setup) . )

( red breakLoop(('msd1 ! 'x1) ;; ('msd2 ! 'x1) ;; ('msd2 ! 'v1) ;; ('msd1 ! 'v1), (simulationUnits externalConnection stepSize: 1 endTime: 1 Algorithm: emptyList SNSet: emptySUSet guessOn: emptyLoop) , R) . )

( red getSUs(('msd1 ! 'x1) ;; ('msd2 ! 'x1) ;; ('msd2 ! 'v1) ;; ('msd1 ! 'v1), emptySUSet) .)

( red findStep(setup,setup) . )
( red getPorts(simulationUnits, emptyLoop) . )

set trace on .
trace exclude FULL-MAUDE . set trace substitution off . set trace eq off . 