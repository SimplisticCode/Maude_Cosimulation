load Scenario
*** Do things to start
(omod COSIMULATION is 
   protecting StepFinder + SCENARIO .

   op performStepNegotiation : Configuration -> Configuration .
   op performGlobalStep : Configuration -> Configuration .
   op allSUsinSimulation : OidSet Configuration -> Bool .
   op simulationStepDone : OidSet Configuration Nat -> Bool .
   op allSUsinUnloaded : OidSet Configuration -> Bool .
   op initializationDone : OidSet Configuration -> Bool .
  
   vars ID1 ID2 : SUID .
   var OCH : Oid .
   vars INPUT OUTPUT : PortId .
   vars SUTIME INPUTTIME TIME : Nat .
   var VALUE : FMIValue .
   vars INPUTS OUTPUTS CONF STARTCONF SCENARIOMODEL CONF1 SUS : Configuration . 
   var STATE : fmiState .
   vars ALGO STEPNEGOALGO INIT TERM : ActionList .
   vars FEEDTHROUGH SUIDs : OidSet .
   var SUIDsNE : NeOidSet . 
   vars STEPSIZE NEGOTIATEDSTEPSIZE FUTURETIME ENDTIME : NzNat .
   var LOOP : AlgebraicLoop .  
   vars LOOPS SOLVEDLOOPS LOOPTOSOLVE : AlgebraicLoopSet .  
   vars PORTVALUES : PortValueMap .
   var SUINSTANCE : Object .
   var EVENT : Action .
   vars STATE1 STATE2 : fmiState .
   var CONTRACT : Contract .

  *** Find instrumentation
  *** Succedes if there is a contract 
  ***
  eq simulationStepDone((ID1, SUIDs), < ID1 : SU | time : TIME, inputs : INPUTS, outputs : OUTPUTS > CONF, TIME) = TIME > 0 and allDef(INPUTS, TIME) and allDef(OUTPUTS, TIME) and simulationStepDone(SUIDs, CONF, TIME) .
  eq simulationStepDone(empty, CONF, TIME) = true .

  eq initializationDone((ID1, SUIDs), < ID1 : SU | inputs : INPUTS, outputs : OUTPUTS > CONF) = allDef(INPUTS, 0) and allDef(OUTPUTS, 0) and initializationDone(SUIDs, CONF) .
  eq initializationDone(empty, CONF) = true .

  *** Step Negotiation
  ceq performStepNegotiation(CONF 
                        < OCH : OrchestrationData | actualStepSize : STEPSIZE, SNSet : SUIDsNE, Algorithm : ALGO > ) = 
                        < OCH : OrchestrationData | actualStepSize : NEGOTIATEDSTEPSIZE, SNSet : empty, Algorithm : (ALGO ; EVENT) > CONF
                              if STARTCONF := CONF < OCH : OrchestrationData | >
                              /\ < NEGOTIATEDSTEPSIZE ++ STEPNEGOALGO > := findStep(SUIDsNE, STARTCONF, STARTCONF, emptyList)
                              /\ EVENT := complexEvent: StepNegotiation SaveSUs: SUIDsNE RestoreSUs: SUIDsNE Actions: STEPNEGOALGO .

  eq performStepNegotiation(CONF < OCH : OrchestrationData | SNSet : empty > ) 
                              = CONF < OCH : OrchestrationData | > .  

  *** All SUS are in the simulation fmistate
  eq allSUsinSimulation((ID1, SUIDs), < ID1 : SU | fmistate : STATE > SUS) = STATE == Simulation and allSUsinSimulation(SUIDs, SUS) . 
  eq allSUsinSimulation(empty, CONF) = true .

  *** All SUS unloaded
  eq allSUsinUnloaded((ID1, SUIDs), < ID1 : SU | fmistate : STATE > SUS) = STATE == Unloaded and allSUsinUnloaded(SUIDs, SUS) . 
  eq allSUsinUnloaded(empty, CONF) = true .

  ***SUS -> fmiState -> fmiState -> SUS
  op moveState : OidSet Configuration fmiState fmiState -> Configuration .
  eq moveState((ID1, SUIDs), < ID1 : SU | fmistate : STATE1 > SUS, STATE1, STATE2) = 
     < ID1 : SU | fmistate : STATE2 > moveState(SUIDs, SUS, STATE1, STATE2) .
  eq moveState(empty, CONF, STATE1, STATE2) = CONF .

  op moveToInitialize : OidSet Configuration -> Configuration .
  eq moveToInitialize((ID1, SUIDs), < ID1 : SU | inputs : INPUTS , outputs : OUTPUTS, fmistate : ExperimentSetup > SUS) = 
     < ID1 : SU | fmistate : Initialize, inputs : undefPorts(INPUTS, 0), outputs : undefPorts(OUTPUTS, 0) > moveToInitialize(SUIDs, SUS) .
  eq moveToInitialize(empty, CONF) = CONF .

  *** FMI-standard fmistate transition:
  *** Initialize all - to have fewer states
  rl [setupExp] : { < ID1 : SU | fmistate : Instantiated > CONF
                   < OCH : OrchestrationData | Initialization : ALGO, suids : SUIDsNE > }
                 => { moveState(SUIDsNE, < ID1 : SU | > CONF, Instantiated, ExperimentSetup)
                    < OCH : OrchestrationData | Initialization : (ALGO ; (fmiEvent: CreateExp SUs: SUIDsNE)) > } .


  crl [init] : { < ID1 : SU | fmistate : ExperimentSetup > CONF
                  < OCH : OrchestrationData | Initialization : ALGO, suids : SUIDsNE > } 
           => { moveToInitialize(SUIDsNE, < ID1 : SU | > CONF)
                    < OCH : OrchestrationData | Initialization : (ALGO ; EVENT) > } 
          if EVENT := fmiEvent: EnterInitialization SUs: SUIDsNE .
           

  crl [simulate] : { < ID1 : SU | fmistate : Initialize > CONF
                    < OCH : OrchestrationData | Initialization : ALGO, suids : SUIDsNE, valuesToSet : empty > } 
                => { moveState(SUIDsNE, < ID1 : SU | > CONF, Initialize, Simulation)
                    < OCH : OrchestrationData | Initialization : (ALGO ; EVENT) > }
                if initializationDone(SUIDsNE, < ID1 : SU | fmistate : Initialize > CONF)
                /\ EVENT := fmiEvent: ExitInitialization SUs: SUIDsNE .

  crl [terminate] : { < ID1 : SU | time : ENDTIME, fmistate : Simulation > CONF
                  < OCH : OrchestrationData | Termination : ALGO, endTime : ENDTIME, suids : SUIDsNE > }
                 => { moveState(SUIDsNE, < ID1 : SU | > CONF, Simulation, Terminated)
                  < OCH : OrchestrationData | Termination : (ALGO ; (fmiEvent: Terminate SUs: SUIDsNE)) > } 
                 if simulationStepDone(SUIDsNE, < ID1 : SU | > CONF, ENDTIME) .

  rl [free] : { < ID1 : SU | fmistate : Terminated > CONF
                  < OCH : OrchestrationData | Termination : ALGO, suids : SUIDsNE > }
                 => { moveState(SUIDsNE, < ID1 : SU | > CONF, Terminated, InstanceFreed)
                  < OCH : OrchestrationData | Termination : (ALGO ; (fmiEvent: FreeInstance SUs: SUIDsNE)) > } .

  rl [unload] : { < ID1 : SU | fmistate : InstanceFreed > CONF
                  < OCH : OrchestrationData | Termination : ALGO, suids : SUIDsNE > }
                 => { moveState(SUIDsNE, < ID1 : SU | > CONF, InstanceFreed, Unloaded)
                  < OCH : OrchestrationData | Termination : (ALGO ; (fmiEvent: Unload SUs: SUIDsNE)) > } .

  *** Error handling in Maude

  *** Simulation Procedure
  *** Perform the global simulation step
  *** Set input                  
  ceq performGlobalStep(CONF < ID1 : SU | time : SUTIME, inputs : (< INPUT : Input | time : INPUTTIME, contract : CONTRACT > INPUTS) > 
          < OCH : OrchestrationData | Algorithm : ALGO, valuesToSet : (((ID1 ! INPUT) |-> < TIME ; VALUE >), PORTVALUES) >) = 
      performGlobalStep(CONF SUINSTANCE 
            < OCH : OrchestrationData | Algorithm : (ALGO ; EVENT), valuesToSet : PORTVALUES >)
            if (CONTRACT == reactive and TIME > INPUTTIME and INPUTTIME == SUTIME) or (CONTRACT == delayed and TIME == SUTIME and INPUTTIME < SUTIME)    
            /\ SUINSTANCE := < ID1 : SU | inputs : (< INPUT : Input | value : VALUE, time : TIME > INPUTS) >
            /\ EVENT := portEvent: Set SU: ID1 PId: INPUT .

  *** Step
  ceq performGlobalStep(CONF < ID1 : SU | time : TIME, inputs : INPUTS, outputs : OUTPUTS > 
    < OCH : OrchestrationData | Algorithm : ALGO, actualStepSize : STEPSIZE, endTime : ENDTIME >)
      = 
      performGlobalStep(CONF SUINSTANCE 
              < OCH : OrchestrationData | Algorithm : (ALGO ; EVENT) > )
                        if FUTURETIME := TIME + STEPSIZE 
                        /\ canStep(TIME, FUTURETIME, INPUTS, OUTPUTS) and FUTURETIME <= ENDTIME 
                        /\ SUINSTANCE := stepAction(< ID1 : SU | >, STEPSIZE)
                        /\ EVENT := stepEvent: ID1 StepSize: STEPSIZE . 

  *** Get
  ceq performGlobalStep(CONF < ID1 : SU | outputs : (< OUTPUT : Output | time : TIME, status : Undef, value : VALUE, dependsOn : FEEDTHROUGH > OUTPUTS), inputs : INPUTS >  
  < OCH : OrchestrationData | Algorithm : ALGO, valuesToSet : PORTVALUES > 
        (ID1 ! OUTPUT ==> ID2 ! INPUT)) =
    performGlobalStep(CONF SUINSTANCE (ID1 ! OUTPUT ==> ID2 ! INPUT)
      < OCH : OrchestrationData | Algorithm : (ALGO ; EVENT), valuesToSet : insert((ID2 ! INPUT), < TIME ; VALUE >, PORTVALUES) >)
              if feedthroughSatisfied(FEEDTHROUGH, INPUTS, TIME)
                /\ SUINSTANCE := < ID1 : SU | outputs : (< OUTPUT : Output | status : Def > OUTPUTS) >
                /\ EVENT := portEvent: Get SU: ID1 PId: OUTPUT . 
                      

  *** Algebraic Loop - solve and remove loop
  eq performGlobalStep(CONF 
      < OCH : OrchestrationData | unsolvedSCC : (LOOP , LOOPS), solvedSCC : SOLVEDLOOPS > ) = 
    performGlobalStep(
      solveLoop(CONF < OCH : OrchestrationData | unsolvedSCC : LOOPS, solvedSCC : (LOOP , SOLVEDLOOPS) >, LOOP)) [owise] .

  *** Return the configuration is the simulation is done and update the simulation time
  ceq performGlobalStep(CONF
        < OCH : OrchestrationData | simulationTime : TIME, actualStepSize : STEPSIZE, suids : SUIDsNE >) = 
        CONF < OCH : OrchestrationData | simulationTime : FUTURETIME >
    if FUTURETIME := TIME + STEPSIZE
    /\ simulationStepDone(SUIDsNE, CONF, FUTURETIME) .


  *** Reset and start over if the endtime is not reached, reset loopsSolver and StepNegotiation
  ***ceq performGlobalStep(CONF endTime: ENDTIME) = CONF endTime: ENDTIME
  ***  if simulationStepDone(CONF) and endTime: ENDTIME .

  *** Extract the algortihm and call verify to verify it
 *** crl [get-algortihm]: CONF Algorithm: ALGO Initialization: INIT Termination: TERM => 
 ***                      Algorithm: ALGO Initialization: INIT Termination: TERM
  ***            if simulationDone(CONF) .

  *** Initialization Procedure 
  crl [get-init] :
              < ID1 : SU | fmistate : Initialize, inputs : INPUTS, 
                outputs : (< OUTPUT : Output | status : Undef, value : VALUE, dependsOn : FEEDTHROUGH > OUTPUTS) > 
                      (ID1 ! OUTPUT ==> ID2 ! INPUT)
                      < OCH : OrchestrationData | Initialization : ALGO, valuesToSet : PORTVALUES > 
                  => < ID1 : SU | outputs : (< OUTPUT : Output | status : Def > OUTPUTS) > 
                      (ID1 ! OUTPUT ==> ID2 ! INPUT)
                      < OCH : OrchestrationData | Initialization : (ALGO ; EVENT), valuesToSet : insert((ID2 ! INPUT), < 0 ; VALUE >, PORTVALUES) >  
                if feedthroughSatisfied(FEEDTHROUGH, INPUTS, 0)
                  /\ EVENT := portEvent: Get SU: ID1 PId: OUTPUT . 

  crl [set-init] : < ID1 : SU | inputs : (< INPUT : Input | status : Undef > INPUTS), fmistate : Initialize > 
        < OCH : OrchestrationData | Initialization : ALGO, valuesToSet : (((ID1 ! INPUT) |-> < TIME ; VALUE >), PORTVALUES) >
              => < ID1 : SU | inputs : (< INPUT : Input | status : Def, value : VALUE > INPUTS ) >
                  < OCH : OrchestrationData | Initialization : (ALGO ; EVENT), valuesToSet : PORTVALUES >
                      if EVENT := portEvent: Set SU: ID1 PId: INPUT .

  ***rl [init-loop] : CONF unsolvedSCC: (L ;; LOOPS) => solveLoop(CONF unsolvedSCC: LOOPS, L) .                 

  ***If the scenario contains SUS that require step negotiation
  ***The step negotiation
  crl [stepNego] : { CONF  < OCH : OrchestrationData | SNSet : SUIDsNE > } => 
          { performStepNegotiation(CONF < OCH : OrchestrationData | suids : SUIDsNE > ) }
          if allSUsinSimulation(SUIDsNE, CONF) .
 

  crl [simulationStep]: { CONF < OCH : OrchestrationData | SNSet : empty, simulationTime : TIME, endTime : ENDTIME, suids : SUIDsNE > } => 
          { 
            performGlobalStep(CONF < OCH : OrchestrationData | > ) 
          }
          if allSUsinSimulation(SUIDsNE, CONF) and TIME < ENDTIME .

  op init : -> GlobalState .
  eq init = { scenario(externalConnection, simulationUnits) } .

  ops initConfiguration initConfigurationUninstrumented initConfDSE : -> Configuration .
  eq initConfiguration = scenario(externalConnection, simulationUnits) .
  eq initConfigurationUninstrumented = scenario(externalConnection, simulationUnitsNotInstrumented) .
  eq initConfDSE = externalConnection simulationUnitsNotInitialized .

  op isInitialState : Configuration -> Bool .
  eq isInitialState(CONF < OCH : OrchestrationData | >) = false .
  eq isInitialState(CONF) = true [owise] .

  op getOrchestrator : Configuration -> Configuration .
  eq getOrchestrator(CONF < OCH : OrchestrationData | >) = < OCH : OrchestrationData | > .
  eq getOrchestrator(CONF) = none [owise] .

  crl [getAlgortihm]: { SCENARIOMODEL } => { getOrchestrator(CONF1) }
      if isInitialState(SCENARIOMODEL)
      /\ LOOPS := tarjan(SCENARIOMODEL)
      /\ SUIDsNE := getSUIDsOfScenario(SCENARIOMODEL)
      /\ CONF := calculateSNSet(SCENARIOMODEL initialOrchestrationData(1,1,LOOPS,SUIDsNE)) 
      /\ {CONF} => { CONF1 } 
      /\ allSUsinUnloaded(SUIDsNE, CONF1) .


endom)

eof

(show all .)
q 

*** Command to get all the configurations
( search init =>* {C:Configuration} .)


( red isInitialState(("ctrl" ! "valveState" ==> "tank" ! "valveState")(
"tank" ! "waterlevel" ==> "ctrl" ! "waterlevel")< "ctrl" : SU | canReject : false,fmistate : Instantiated,inputs : < "waterlevel" : Input | contract :
    delayed,status : Undef,time : 0,type : integer,value : < 0 > >,outputs : < "valveState" : Output | dependsOn : empty,status : Undef,time : 0,type :
    integer,value : < 0 > >,parameters : none,path : "",time : 0 > < "tank" : SU | canReject : false,fmistate : Instantiated,inputs : < "valveState" :
    Input | contract : delayed,status : Undef,time : 0,type : integer,value : < 0 > >,outputs : < "waterlevel" : Output | dependsOn : empty,status :
    Undef,time : 0,type : integer,value : < 0 > >,parameters : none,path : "",time : 0 >) .)

( frew [100] setup . )

( red moveState(setup, Instantiated, ExperimentSetup) . )

(red allSUsinUnloaded(< "ctrl" : SU | canReject : false,fmistate : Unloaded,inputs : < "waterlevel" : Input | contract : delayed,status : Def,time : 1,type : integer,value : < 2546248239 > >,outputs : < "valveState" : Output | dependsOn : empty,status : Def,time : 1,type : integer,value : < 2546248239 > >,parameters : none,path : "",time : 1 > < "tank" : SU | canReject : false,fmistate : Unloaded,inputs : < "valveState" : Input | contract : delayed,status : Def,time : 1,type : integer,value : < 2546248239 > >,outputs : < "waterlevel" : Output | dependsOn : empty,status : Def, time : 1,type : integer,value : < 2546248239 > >,parameters : none,path : "",time : 1 >) . )


(search setup. )

(search in COSIMULATION : setup =>1 C:Configuration . )

show search graph .

(red validScenario(externalConnection, simulationUnits) . )

( red allSUsinSimulation(setup) . )

( red breakLoop(('msd1 ! 'x1) ;; ('msd2 ! 'x1) ;; ('msd2 ! 'v1) ;; ('msd1 ! 'v1), (simulationUnits externalConnection actualStepSize: 1 endTime: 1 Algorithm: emptyList SNSet: empty guessOn: emptyLoop) , R) . )

( red findStep(setup,setup) . )
( red getAllPorts(simulationUnits, empty) . )

set trace on .
trace exclude FULL-MAUDE . set trace substitution off . set trace eq off . 

(red moveState(("ctrl" ! "valveState" ==> "tank" ! "valveState")(
"tank" ! "waterlevel" ==> "ctrl" ! "waterlevel")< "ctrl" : SU | canReject : false,fmistate : Instantiated,inputs : < "waterlevel" : Input |
    contract : delayed,status : Undef,time : 0,type : integer,value : < 0 > >,outputs : < "valveState" : Output | dependsOn : empty,status :
    Undef,time : 0,type : integer,value : < 0 > >,parameters : none,path : "",time : 0 > < "tank" : SU | canReject : false,fmistate :
    Instantiated,inputs : < "valveState" : Input | contract : delayed,status : Undef,time : 0,type : integer,value : < 0 > >,outputs : <
    "waterlevel" : Output | dependsOn : empty,status : Undef,time : 0,type : integer,value : < 0 > >,parameters : none,path : "",time : 0 >,
    Instantiated, ExperimentSetup ) . )