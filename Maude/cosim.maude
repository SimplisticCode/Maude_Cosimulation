load StepFinder

(omod COSIMULATION is 
   protecting StepFinder .
   sort GlobalState .

   op performStepNegotiation : Configuration -> Configuration .
   op performGlobalStep : Configuration -> Configuration .
   op allSUinSimulation : Configuration -> Bool .
   op `{_`} : Configuration -> GlobalState .
   op getAllPorts : Configuration EPortIdSet -> EPortIdSet .
   op simulationStepDone : Configuration -> Bool .
   op getActionEnabled : Configuration Configuration Nat -> PortIdSet .
   op setActionEnabled : Configuration SUID Nat Configuration -> PortIdSet .


   vars ID1 ID2 : SUID .
   var SUIDs : SUIdSet .
   vars INPUT OUTPUT : PortId .
   vars TIME TIME1 TIME2 : Nat .
   vars INPUTS OUTPUTS OUTPUTSSETABLE CONF : Configuration . 
   var STATE : fmiState .
   var ALGO : ActionList .
   var FEEDTHROUGH : PortIdSet .
   var PORTS : EPortIdSet .
   vars STEPSIZE FUTURETIME ENDTIME : NzNat .
   var LOOP : Algebraic .  
   var LOOPS : AlgebraicLoops .  
   var INPUTTYPE : Mode .
   var STEPRes : findStepPair .

  eq simulationStepDone(< ID1 : SU | time : TIME, inputs : INPUTS, outputs : OUTPUTS > CONF) = TIME > 0 and allDef(INPUTS, TIME) and allDef(OUTPUTS, TIME) and simulationStepDone(CONF) .
  eq simulationStepDone(CONF) = true [owise] .

  *** Step Negotiation
  ceq performStepNegotiation(CONF SNSet: SUIDs stepSize: STEPSIZE Algorithm: ALGO) = 
                              CONF SNSet: emptySUSet 
                              Algorithm: (ALGO ; (complexEvent: StepNegotiation SaveSUs: SUIDs RestoreSUs: SUIDs Actions: getAL(STEPRes)))
                              stepSize: getStep(STEPRes)
                              if needsStepNegotiation(SUIDs) /\ STEPRes := findStep(SUIDs, CONF stepSize: STEPSIZE, CONF stepSize: STEPSIZE, emptyList) .

  ceq performStepNegotiation(CONF SNSet: SUIDs) = CONF if not needsStepNegotiation(SUIDs) .  

  *** Get All ports in the scenario:
  ceq getAllPorts(< ID1 : SU | inputs : (< INPUT : Input | > INPUTS) > CONF, PORTS) = getAllPorts(< ID1 : SU | > CONF,  (ID1 ! INPUT) ;; PORTS )
            if not (ID1 ! INPUT) in PORTS .

  ceq getAllPorts(< ID1 : SU | outputs : (< OUTPUT : Output | > OUTPUTS) > CONF, PORTS) = getAllPorts(< ID1 : SU | > CONF,  (ID1 ! OUTPUT) ;; PORTS )
            if not (ID1 ! OUTPUT) in PORTS .

  eq getAllPorts(CONF, PORTS) = PORTS [owise] . 

  *** All SUs are in the simulation state
  eq allSUinSimulation(< ID1 : SU | state : STATE > CONF) = STATE == Simulation and allSUinSimulation(CONF) . 
  eq allSUinSimulation(CONF) = true [owise] .

  eq getActionEnabled(none, INPUTS, TIME) = emptySet . 
  ceq getActionEnabled(< OUTPUT : Output | status : Undef, dependsOn : FEEDTHROUGH > OUTPUTS, INPUTS, TIME) = OUTPUT :: getActionEnabled(OUTPUTS, INPUTS, TIME)
        if feedthroughSatisfied(FEEDTHROUGH, INPUTS, TIME) .

  eq getActionEnabled(< OUTPUT : Output | > OUTPUTS, INPUTS, TIME) = getActionEnabled(OUTPUTS, INPUTS, TIME) [owise] .

  eq setActionEnabled(none, ID1, TIME, CONF) = emptySet .
  ceq setActionEnabled(< INPUT : Input | time : TIME1, type : INPUTTYPE > INPUTS, ID1, TIME,
                                CONF < ID2 : SU | outputs : (< OUTPUT : Output | time : TIME2, status : Def > OUTPUTS) > (ID2 ! OUTPUT ==> ID1 ! INPUT)) 
                        = INPUT :: setActionEnabled(INPUTS, ID1, TIME, CONF < ID2 : SU | outputs : (< OUTPUT : Output | time : TIME2, status : Def > OUTPUTS) > (ID2 ! OUTPUT ==> ID1 ! INPUT)) 
                        if TIME1 < TIME2 and ((INPUTTYPE == d and TIME1 < TIME) or (INPUTTYPE == r and TIME == TIME1)) .

  eq setActionEnabled(< INPUT : Input | > INPUTS, ID1, TIME, CONF) = setActionEnabled(INPUTS, ID1, TIME, CONF) [owise] .

  *** Perform the global simulation step
  *** Set input
  ceq performGlobalStep(CONF < ID1 : SU | time : TIME, inputs : (< INPUT : Input | time : TIME1, type : INPUTTYPE > INPUTS) > 
                          < ID2 : SU | outputs : (< OUTPUT : Output | time : TIME2, status : Def > OUTPUTS) > 
                         (ID2 ! OUTPUT ==> ID1 ! INPUT) Algorithm: ALGO) = 
            performGlobalStep(CONF setAction( < ID1 : SU | >, INPUT, TIME2) < ID2 : SU | > 
                         (ID2 ! OUTPUT ==> ID1 ! INPUT) 
                         Algorithm: (ALGO ; (event: Set SU: ID1 PId: INPUT)))
                         if TIME1 < TIME2 and ((INPUTTYPE == d and TIME1 < TIME) or (INPUTTYPE == r and TIME == TIME1)) .

  *** Step
  ceq performGlobalStep(CONF < ID1 : SU | time : TIME, inputs : INPUTS, outputs : OUTPUTS > stepSize: STEPSIZE endTime: ENDTIME Algorithm: ALGO)  = 
      performGlobalStep(CONF 
                        stepAction(< ID1 : SU | >, STEPSIZE)
                        stepSize: STEPSIZE endTime: ENDTIME Algorithm: (ALGO ; (event: Step SU: ID1 PId: emptySet)))
                        if FUTURETIME := TIME + STEPSIZE /\ canStep(TIME, FUTURETIME, INPUTS, OUTPUTS) and FUTURETIME <= ENDTIME . 

  *** Get
    ceq performGlobalStep(CONF < ID1 : SU | time : TIME, inputs : INPUTS, outputs : OUTPUTS >  Algorithm: ALGO) =
        performGlobalStep(CONF getAction(< ID1 : SU | >, getActionEnabled(OUTPUTS, INPUTS, TIME))
                         Algorithm: (ALGO ; (event: Get SU: ID1 PId: getActionEnabled(OUTPUTS, INPUTS, TIME)))) 
                         if getActionEnabled(OUTPUTS, INPUTS, TIME) =/= emptySet .

  *** Algebraic Loop - solve and remove loop
  eq performGlobalStep(CONF SCC: (LOOP # LOOPS)) = solveLoop(CONF SCC: LOOPS, LOOP) [owise] .

  *** 
  ceq performGlobalStep(CONF) = CONF if simulationStepDone(CONF) .

  *** FMI-standard state transition:
  rl [setExp] : < ID1 : SU | state : Instantiated > Algorithm: ALGO 
                 => < ID1 : SU | state : ExperimentSetup > Algorithm: (ALGO ; (fmiEvent: CreateExp SU: ID1)) .

  rl [init] : < ID1 : SU | inputs : INPUTS , outputs : OUTPUTS, state : ExperimentSetup >  Algorithm: ALGO 
           => < ID1 : SU | inputs : undefPorts(INPUTS, 0), outputs : undefPorts(OUTPUTS, 0), state : Initialize > 
              Algorithm: (ALGO ; (fmiEvent: EnterInitialization SU: ID1)).

  crl [simulate] : < ID1 : SU | inputs : INPUTS , outputs : OUTPUTS, state : Initialize > CONF Algorithm: ALGO 
                => < ID1 : SU | state : Simulation > CONF Algorithm: (ALGO ; (fmiEvent: ExitInitialization SU: ID1))
                if allDef(INPUTS, 0) and allDef(OUTPUTS, 0) and allInputsSet(CONF) .

  crl [terminate] : < ID1 : SU | time : TIME, inputs : INPUTS , outputs : OUTPUTS , state : Simulation > endTime: TIME Algorithm: ALGO 
                 => < ID1 : SU | state : Terminated > endTime: TIME Algorithm: (ALGO ; (fmiEvent: Terminate SU: ID1))
                 if allDef(OUTPUTS, TIME) and allDef(INPUTS, TIME) .

  rl [free] : < ID1 : SU | state : Terminated > Algorithm: ALGO 
           => < ID1 : SU | state : InstanceFreed > Algorithm: (ALGO ; (fmiEvent: FreeInstance SU: ID1)) .

  rl [unload] : < ID1 : SU | state : InstanceFreed > Algorithm: ALGO
              => < ID1 : SU | state : Unloaded > Algorithm: (ALGO ; (fmiEvent: Unload SU: ID1)) .


   *** Initalization Procedure 
   crl [get-init] :  < ID1 : SU | outputs : ((< OUTPUT : Output | status : Undef , dependsOn : FEEDTHROUGH >) OUTPUTS) , inputs : INPUTS, state : Initialize > Algorithm: ALGO 
                  => 
                  getAction(< ID1 : SU | >, OUTPUT)                  
                  Algorithm: (ALGO ; (event: Get SU: ID1 PId: OUTPUT)) if feedthroughSatisfied(FEEDTHROUGH, INPUTS, 0) .

   *** Inputs can only rely on one connection 
   rl [set-init] :  < ID1 : SU | inputs : (< INPUT : Input | status : Undef > INPUTS), state : Initialize > Algorithm: ALGO 
                    < ID2 : SU | outputs : (< OUTPUT : Output | status : Def > OUTPUTS) > (ID2 ! OUTPUT ==> ID1 ! INPUT)
                        => 
                           setAction(< ID1 : SU | >, INPUT, 0)
                           < ID2 : SU | > (ID2 ! OUTPUT ==> ID1 ! INPUT) Algorithm: (ALGO ; (event: Set SU: ID1 PId: INPUT)) .


  ***rl [init-loop] : CONF SCC: (L # LOOPS) => solveLoop(CONF SCC: LOOPS, L) .                 

 ***Complex scenarios with save and restore - this should 

  ***If the scenario contains SUs that require step negotiation
  ***The step negotiation
  crl [stepNego] : { CONF SNSet: SUIDs} => 
          { performStepNegotiation(CONF SNSet: SUIDs)}
          if needsStepNegotiation(SUIDs) and allSUinSimulation(CONF) .


  crl [simulationStep]: { CONF SNSet: SUIDs} => 
          { performGlobalStep(CONF SNSet: SUIDs) }
        if (not needsStepNegotiation(SUIDs)) and allSUinSimulation(CONF) .

   *** Definition of system
  op externalConnection : ->  Configuration .    *** External Connections - all ports have unique name
  op simulationUnits : -> Configuration .      *** SUs - ID * input ports * and outputs


***Encoding of the Scenario 
eq externalConnection = ('env ! 'psu ==> 'plant ! 'psu) ('env ! 'ref ==> 'ctrl ! 'ref) ('load ! 'xaft ==> 'ctrl ! 'xaft) ('ctrl ! 'o ==> 'plant ! 'o) ('plant ! 'w ==> 'ctrl ! 'w) ('load ! 'x ==> 'plant ! 'x) ('load ! 'v ==> 'plant ! 'v) ('plant ! 'f ==> 'load ! 'f) .

eq simulationUnits = (< 'env : SU | time : 0, inputs : none, outputs : (< 'ref : Output | time : 0, status : Undef, dependsOn : emptySet >) (< 'psu : Output | time : 0, status : Undef, dependsOn : emptySet >), state : Instantiated, canReject : false >)

 	 (< 'load : SU | time : 0, inputs : (< 'f : Input | time : 0, type : d, status : Undef  >), outputs : (< 'v : Output | time : 0, status : Undef, dependsOn : emptySet >) (< 'xaft : Output | time : 0, status : Undef, dependsOn : emptySet >) (< 'x : Output | time : 0, status : Undef, dependsOn : emptySet >), state : Instantiated, canReject : false >)

 	 (< 'ctrl : SU | time : 0, inputs : (< 'ref : Input | time : 0, type : d, status : Undef  >) (< 'w : Input | time : 0, type : d, status : Undef  >) (< 'xaft : Input | time : 0, type : d, status : Undef  >), outputs : (< 'o : Output | time : 0, status : Undef, dependsOn : emptySet >), state : Instantiated, canReject : false >)

 	 (< 'plant : SU | time : 0, inputs : (< 'x : Input | time : 0, type : r, status : Undef  >) (< 'v : Input | time : 0, type : r, status : Undef  >) (< 'o : Input | time : 0, type : d, status : Undef  >) (< 'psu : Input | time : 0, type : r, status : Undef  >), outputs : (< 'w : Output | time : 0, status : Undef, dependsOn : ( 'v ) >) (< 'f : Output | time : 0, status : Undef, dependsOn : ( 'v :: 'x ) >), state : Instantiated, canReject : false >) .


endom)

eof

*** Check for termination - not useful 
(search setup =>! X:Configuration . )
show search graph .

( frew [20] setup . )

( frew [20] setup using fmi_rules_setExpt . )
( red needsStepNegotiation(setup) . )
( red allSUinSimulation(setup) . )

( red breakLoop(('msd1 ! 'x1) ;; ('msd2 ! 'x1) ;; ('msd2 ! 'v1) ;; ('msd1 ! 'v1), (simulationUnits externalConnection stepSize: 1 endTime: 1 Algorithm: emptyList SNSet: emptySUSet guessOn: emptyLoop) , R) . )

( red getSUs(('msd1 ! 'x1) ;; ('msd2 ! 'x1) ;; ('msd2 ! 'v1) ;; ('msd1 ! 'v1), emptySUSet) .)

( red findStep(setup,setup) . )
( red getAllPorts(simulationUnits, emptyLoop) . )

set trace on .
trace exclude FULL-MAUDE . set trace substitution off . set trace eq off . 