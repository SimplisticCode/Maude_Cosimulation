
\section{Synthesize of instrumentations and Parameters}\label{sc:DSE}
Using Maude's integrated LTL model checker, the Maude framework enables different kinds of design space exploration.
Design space exploration allows the co-simulation practitioner to explore how different model parameters/designs change the behavior of the system/simulation result.
The framework enables a co-simulation practitioner to experiment with different valuations of the scenario's instrumentation and parameters of the SUs.
We describe the two independently even though they can be combined.


\subsection{Instrumentation of a Scenario}
The instrumentation of the input ports is used to achieve more accurate co-simulation results as shown in \cite{Gomes2019,Oakes2021,hansen_verification_2021}.
However, finding the instrumentation that results in the most accurate simulation can be challenging since none of the existing tools to export FMUs/SUs provide this information.

We show that one can use Maude's model checker to explore the consequences of the different instrumentations of a scenario.

To explore the different instrumentations of a scenario, we define the contract of an input \texttt{noContract}, which can be used when the user does not know the input's instrumentation.
A scenario is \emph{instrumented} if none of the inputs are annotated with the contract \texttt{noContract}, otherwise the scenario is \emph{partially instrumented}.
The scenario in \cref{ex:simulationunits} is instrumented, while the scenario below is partially instrumented:
\scriptsize
\begin{alltt}
eq simulationUnitsNotInstrumented = 
< "tank" : SU | parameters : ("flow" |-> <\,100\,>),  localState : ("waterlevel" |-> <\,0\,>),
                time : 0,  fmistate : Instantiated, canReject : false, 
                inputs : (< "valveState" : Input | value : <\,0\,>, type : integer, time : 0,
                                                    contract : noContract, status : Undef >), 
                outputs : (< "waterlevel" : Output | value : <\,0\,>, type : integer, time : 0,
                                                      status : Undef, dependsOn : empty >) >

< "ctrl" : SU | parameters : (("high" |-> <\,5\,>) , ("low" |-> <\,0\,>)), canReject : false, 
                localState : ("valve" |-> <\,false\,>), fmistate : Instantiated, time : 0, 
                inputs : (< "waterlevel" : Input | value : <\,0\,>, type : integer, time : 0,
                                                    contract : noContract, status : Undef >), 
                outputs : (< "valveState" : Output | value : <\,0\,>, type : integer, time : 0,
                                                      status : Undef, dependsOn : empty >) > . 

\end{alltt}
\normalsize


The execution of an algorithm described in \cref{sc:synthesize} is only defined for instrumented scenarios.

Defining a contract to be \texttt{noContract} means that we can define a rewrite rule that non-deterministically assigns the input port to be either \texttt{delayed} or \texttt{reactive}:
\scriptsize
\begin{alltt}
rl [instr-delayed]: 
  findInstr(< SU1 : SU | inputs : (< I : Input | contract : noContract > IS) > C)
  =>
  findInstr(< SU1 : SU | inputs : (< I : Input | contract : delayed > IS) > C) .

rl [instr-reactive]: 
  findInstr(< SU1 : SU | inputs : (< I : Input | contract :   noContract > IS) > C)
  =>
  findInstr(< SU1 : SU | inputs : (< I : Input | contract : reactive > IS) > C) .

crl [remove-findInstr]: findInstr(CONF) => CONF 
  if scenarioInstrumented(CONF) .
\end{alltt}
\normalsize
The rule applies if the scenario has been wrapped inside the constructor \texttt{findInstr}.
The rule find an input \texttt{I} of an SU \texttt{SU1} with the contract \texttt{noContract} and makes the input either \texttt{delayed} or \texttt{reactive}.
Maude keeps instrumenting the scenario until the scenario is instrumented and satisfies the equational predicate \texttt{scenarioInstrumented}, where the rule \texttt{remove-findInstr} free the instrumented scenario from the constructor \texttt{findInstr} such the the other rewrite rules can start rewriting it.

The different instrumentations of a partially instrumented scenario is found and explored using the rule:

\small
\begin{alltt}
crl [findInstrumentation]: findContacts(INIT) => CONF
    if findInstr(INIT) => CONF
    \(\land\) empty == tarjan(CONF) *** No Algebraic loops
    \(\land\) runAnyAlgorithm CONF => run: ORC on: FINAL with: SIMDATA
    \(\land\) shouldSatisfy(FINAL) .
\end{alltt}
\normalsize
The rule \texttt{findInstrumentation} generates an instrumented scenario \texttt{CONF} from the partially instrumented scenario \texttt{INIT}.
The instrumented scenario \texttt{CONF} is simulated using the rule \texttt{runAnyAlgorithm} to explore its simulation result.

The explored instrumentations can be configured in desired ways by defining specific properties that should satisfy.
For example, in the rule above, it is specified using \texttt{empty == tarjan(CONF)} that none of the instrumentations must introduce any algebraic loops in the scenario.

The instrumentations of interest can be selected based on the simulation it produces. 
This is done in the above rule, where we have stated that the simulation above must satisfy the predicate \texttt{shouldSatisfy} that says the water level of the tank in the final state should be in a specific range.

\begin{example}
We use Maude to find all instrumentations of the presented partially instrumented scenario that satisfies criteria above using the search command: 
\small
\begin{alltt}
search findContracts(simulationUnitsNotInstrumented) =>! C:Configuration .
\end{alltt}
Returning the following three instrumentations:
\begin{alltt}
Solution 1
C:Configuration --> (
< "ctrl" : SU | inputs : < "waterlevel" : Input | contract : delayed > >
< "tank" : SU | inputs : < "valveState" : Input | contract : reactive > >

Solution 2
C:Configuration --> (
< "ctrl" : SU | inputs : < "waterlevel" : Input | contract : reactive > >
< "tank" : SU | inputs : < "valveState" : Input | contract : delayed > >

Solution 3
C:Configuration --> (
< "ctrl" : SU | inputs : < "waterlevel" : Input | contract : delayed > >
< "tank" : SU | inputs : < "valveState" : Input | contract : delayed > >

\end{alltt}
\normalsize
The search command returns a scenario; we have removed everything except the instrumentation of the inputs.
\end{example}

\subsection{Finding the SU Parameters}
An SU may have different parameters affecting its behavior.
This means that different parameters potentially lead to different simulation results.
Design space exploration explores how different valuations of the parameters affect the co-simulation result to find the optimal design of the system. 

The Maude framework allows design space exploration by letting the user specify the valuation of a parameter as a set of possible valuations inside the \texttt{choose}-operator.
The \texttt{choose}-operator non-deterministically selects an element in the set.

\begin{example}\label{ex:dse}.
  Assuming that we want find the parameter \texttt{flow} of the water tank example such that the final state satisfies the predicate below:
  \scriptsize
  \begin{alltt}
op above10 : Configuration -> Bool .
eq above10(CONF < "tank" : SU | localState : ( "waterlevel" |-> < V >) >) = V > 10 .  
  \end{alltt}
  \normalsize
This predicate says the water level should be higher than 10.
We want to explore the following valuations of the parameter \texttt{flow}: 1, 2 and 30:
\small
\begin{alltt}
< "tank" : SU | parameters : ("flow" |-> choose((< 1 >,< 2 >,< 30 >))) >
\end{alltt}
\normalsize
We use the rule below to select different parameter valuations to see if they result in a simulation that satisfies the predicate \texttt{above10}:
\small
\begin{alltt}
  crl [dse] : selectParams(UNITIALIZEDCONF) => CONF 
  if UNITIALIZEDCONF => CONF
  \(\land\) runAnyAlgorithm CONF => 
      run: < ALG : AlgData | Initialization : emptyList, 
      CosimStep : emptyList, Termination : emptyList > 
      on: FINALSTATE
      with: SIMULATIONDATA
  \(\land\) above10(FINALSTATE) .
\end{alltt}
\normalsize
\texttt{selectParams} is a constructor to ensure that no other rewrite rules can rewrite \texttt{UNITIALIZEDCONF}.
The different valuations are explored using the search command:
\small
\begin{alltt}
  search selectParams(ScenarioDSE)  =>! C:Configuration .
\end{alltt}
\normalsize
Where \texttt{ScenarioDSE} is the scenario from \cref{ex:simulationunits} where the parameter \texttt{flow} is defined using the \texttt{choose}-operator as shown above.
The search exhaustively checks all valuations and returns the valuations that satisfy the predicate \texttt{above10}:
\small
\begin{alltt}
C:Configuration --> 
  < "tank" : SU | parameters : "flow" |-> < 30 > >
\end{alltt}
\normalsize
The search again returns a whole scenario, but above we only show the selected valuation for the parameter \texttt{flow}.
\end{example}

\subsection{Combining Design Space Exploration of the Instrumentation and Parameters}
We can combine the two searches described above to simultaneously  experiment with the instrumentation and the parameters of a scenario.

\begin{example}
  We can explore different instrumentations and parameters by defining a partially instrumented scenario with changeable parameters:
\scriptsize
\begin{alltt}
eq simulationDSE =  
< "tank" : SU | parameters : ("flow" |-> choose((< 1 >,< 2 >,< 30 >))), 
localState : ( "waterlevel" |-> < 0 > ) , time : 0, 
inputs : (< "valveState" : Input | value : < 0 >, type : integer, time : 0, 
                                   contract : noContract, status : Undef  >), 
outputs : (< "waterlevel" : Output | value : < 0 >, type : integer, time : 0, 
                                     status : Undef, dependsOn : empty >), 
fmistate : Instantiated, canReject : false >

< "ctrl" : SU | parameters : (("high" |-> < 20 >) , ("low" |-> < 0 >)), 
localState : ( "valve" |-> < false >), time : 0, 
inputs : (< "waterlevel" : Input | value : < 0 >, type : integer, time : 0, 
                                   contract : noContract, status : Undef  >), 
outputs : (< "valveState" : Output | value : < 0 >, type : integer, time : 0, 
                                     status : Undef, dependsOn : empty >), 
fmistate : Instantiated, canReject : false > .
\end{alltt}
\normalsize
The scenario can be explored using the search command from \cref{ex:dse}. 
%The search finds that the following instrumentations and parameters satisfy 
\end{example}


% \subsection{Limitation of the approach}
% We have performed the analyses on different scenarios. 
% The state-space explosion is, of course, also applying to our work. 
% However, we have been able to find the correct instrumentation and the contracts on systems with \simon{Create big scenario and test} SUs.

% Also, we do not currently support adaptive co-simulations~\cite{Inci2021} where the instrumentation changes during the simulation. 
% However, this is our impression that this feature trivial can be added.